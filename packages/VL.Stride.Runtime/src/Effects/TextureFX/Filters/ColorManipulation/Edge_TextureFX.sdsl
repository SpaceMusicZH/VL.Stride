// original code:
// https://github.com/mrvux/dx11-vvvv-girlpower/blob/master/nodes/texture11/Filter/Edge.tfx

shader Edge_TextureFX : TextureFX
{
    float Bright = 1.0f;
    float Radius = 1.0f;

    float4 q(float2 uv,float2 off,float v)
    {
        return Texture0.SampleLevel(LinearSampler, uv + off, v);
    }

    stage override float4 Shading()
    {
        float2 uv = streams.TexCoord;
        
        float rad = max(Radius, 0);
        float ts = max(Texture0TexelSize.x, Texture0TexelSize.y);
        float3 e = float3(1, -1, 0) * rad * ts;
        float v = log2(rad);

        float4 cx = q(uv, e.xy, v) + q(uv, e.xz, v) + q(uv, e.xx, v) - q(uv, e.yy, v) - q(uv, e.yz ,v) - q(uv, e.yx, v);
        float4 cy = q(uv, e.yy ,v) + q(uv, e.zy, v) + q(uv, e.xy, v) - q(uv, e.yx, v) - q(uv, e.zx, v) - q(uv, e.xx, v);
        
        float4 col = sqrt(cx*cx+cy*cy) * Bright * pow(2, rad*ts)/sqrt(saturate(rad) + .001);
        col.a = Texture0.SampleLevel(LinearSampler, uv, 0).a;

        return col;
    }
};